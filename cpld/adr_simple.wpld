header:
Name     adr_simple ;
PartNo   01 ;
Date     24.07.2022 ;
Revision 03 ;
Designer wkla ;
Company  nn ;
Assembly None ;
Location  ;
Device   G16V8 ;

pld:
/* *************** INPUT PINS *********************/
PIN [1..8]   =  [A15..A8]; 
PIN 9   =  PHI2;

/* *************** OUTPUT PINS *********************/
PIN 12   =  CSRAM;
PIN 13   =  CSHIROM;
PIN 15   =  CSIO;
PIN 16   =  CSIO3;
PIN 17   =  CSIO2;
PIN 18   =  CSIO1;
PIN 19   =  CSIO0;
/* *************** LOGIC *********************/

FIELD Addr = [A15..A8];
IOPORT_EQU = Addr:[D000..DFFF];
VIAPORT_EQU = Addr:[D000..D0FF];
ACIAPORT_EQU = Addr:[D100..D1FF];
CSIO2PORT_EQU = Addr:[D200..D2FF];
CSIO3PORT_EQU = Addr:[D300..D3FF];
CSRAM_EQU = Addr:[0000..7FFF];
CSROM_EQU = Addr:[E000..FFFF];

/* RAM */
CSRAM = !CSRAM_EQU # !PHI2;

/* 8kb of ROM */
CSHIROM = !CSROM_EQU;

/* IO */
CSIO= !IOPORT_EQU;
CSIO0 = !VIAPORT_EQU;
CSIO1 = !ACIAPORT_EQU;
CSIO2 = !CSIO2PORT_EQU;
CSIO3 = !CSIO3PORT_EQU;


simulator:
ORDER: A15, A14, A13, A12, A11, A10, A9, A8, PHI2, CSRAM, CSHIROM, CSIO, CSIO0, CSIO1, CSIO2, CSIO3; 

VECTORS:
/* RAM */
0 X X X X X X X 0 H H H H H H H 
0 X X X X X X X 1 L H H H H H H 
/* 8000-CFFF nothing */ 
1 0 X X X X X X X H H H H H H H 
1 1 0 0 X X X X X H H H H H H H 
1 1 0 0 X X X X X H H H H H H H 
/* IO */ 
1 1 0 1 0 0 0 0 X H H L L H H H 
1 1 0 1 0 0 0 1 X H H L H L H H 
1 1 0 1 0 0 1 0 X H H L H H L H 
1 1 0 1 0 0 1 1 X H H L H H H L 
1 1 0 1 0 1 X X X H H L H H H H 
1 1 0 1 1 X X X X H H L H H H H 
/* ROM */
1 1 1 X X X X X X H L H H H H H 
